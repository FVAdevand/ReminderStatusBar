apply plugin: 'com.android.application'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 29
    def env = System.getenv()
    defaultConfig {
        applicationId "ua.fvadevand.reminderstatusbar"
        minSdkVersion 24
        targetSdkVersion 29
        versionCode 6
        versionName "1.0.0"
        applicationVariants.all { variant ->
            variant.outputs.all { output ->
                def project = "reminders"
                def SEP = "_"
                def buildType = variant.variantData.variantConfiguration.buildType.name
                def version = variant.versionName + '.' + versionCode
                def date = new Date()
                def formattedDate = date.format('ddMMyy_HHmm')
                def buildName = env.getOrDefault('BUILD_FILE_NAME', project + SEP + version + SEP + buildType + SEP + formattedDate + ".apk")
                outputFileName = buildName
            }
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            minifyEnabled false
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
}

dependencies {
    def room_version = '2.2.3'
    def lifecycle_version = '2.2.0'
    def androidx_core_version = '1.2.0'
    def coroutines_version = '1.3.0'

    // support
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.1.0-rc02'
    implementation "androidx.preference:preference-ktx:1.1.0"

    // Architecture Components
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.core:core-ktx:$androidx_core_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"
}
